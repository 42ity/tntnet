AC_INIT(tntnet, 1.5.2pre3, [Tommi Maekitalo <tommi@tntnet.org>])
AM_INIT_AUTOMAKE

abi_current=4
abi_revision=1
abi_age=0
sonumber=${abi_current}:${abi_revision}:${abi_age}
AC_SUBST(sonumber)

unset CDPATH

AC_CONFIG_HEADERS([framework/common/config.h])
AC_CONFIG_HEADERS([framework/common/tnt/config.h])
AC_CONFIG_SRCDIR([framework/runtime/tntnet.cpp])

AC_SEARCH_LIBS(gethostbyname, nsl socket resolv)
AC_SEARCH_LIBS(inet_ntop, nsl socket resolv)
AC_CHECK_FUNCS(inet_ntop)

AC_PROG_CXX
AC_DISABLE_STATIC
AC_PROG_LIBTOOL

AC_LANG(C++)

#
# Cxxtools
#
AC_PATH_PROGS(CXXTOOLS_CONFIG, cxxtools-config)
if ! test -x "$CXXTOOLS_CONFIG"; then
  AC_MSG_ERROR([cxxtools configuration script was not found])
fi

CXXTOOLS_CXXFLAGS=`$CXXTOOLS_CONFIG --cxxflags`
CXXTOOLS_LDFLAGS=`$CXXTOOLS_CONFIG --libs`
CXXFLAGS="$CXXFLAGS $CXXTOOLS_CXXFLAGS"
LDFLAGS="$CXXFLAGS $CXXTOOLS_LDFLAGS"

AC_MSG_CHECKING([for cxxtools usability])
AC_COMPILE_IFELSE(
  [#include <cxxtools/net.h>
   void t() { cxxtools::net::Socket s;
              struct sockaddr_storage t = s.getSockAddr(); }
  ],
  AC_MSG_RESULT(yes),
  [
    AC_MSG_RESULT(no)
    AC_MSG_ERROR(please check your cxxtools-installation - maybe you need an upgrade)
  ]
  )

AC_SUBST(CXXTOOLS_CONFIG)
AC_SUBST(CXXTOOLS_CXXFLAGS)
AC_SUBST(CXXTOOLS_LDFLAGS)

AC_CHECK_HEADER([zlib.h], , AC_MSG_ERROR([zlib not found]))
AC_CHECK_HEADERS([limits])

#
# SSL
#
AC_ARG_WITH([ssl],
  AS_HELP_STRING(['--with-ssl=gnutls|openssl|no'], [build with SSL support (default is gnutls)]),
  [ssl_option=$withval],
  [ssl_option=gnutls])

case "$ssl_option" in
    gnutls|yes)
        AC_CHECK_HEADER([gnutls/gnutls.h], [
            AC_MSG_RESULT([building with GnuTLS support])
            AC_DEFINE(WITH_GNUTLS, [], [Define to build with gnutls])
            with_gnutls=1
          ],
          [AC_MSG_ERROR(gnutls-headers not not found)])
        ;;
    openssl)
        AC_CHECK_HEADER([openssl/ssl.h], [
            AC_MSG_RESULT([building with OpenSSL support])
            AC_DEFINE(WITH_OPENSSL, [], [Define to build with openssl])
            with_openssl=1
          ],
          [AC_MSG_ERROR(openssl-headers not not found)])
        ;;
    no)
        AC_MSG_RESULT([building without ssl support])
        ;;
    *)
        AC_MSG_ERROR([unknown ssl-value $ssl_option])
        ;;
esac

AM_CONDITIONAL(MAKE_GNUTLS, test x$with_gnutls = x1)
AM_CONDITIONAL(MAKE_OPENSSL, test x$with_openssl = x1)

#
# others
#
AC_CHECK_FUNCS([setenv])

AC_CONFIG_FILES([
    Makefile
    framework/common/Makefile
    framework/runtime/Makefile
    framework/defcomp/Makefile
    framework/cgi/Makefile
    sdk/tools/common/Makefile
    sdk/tools/ecppc/Makefile
    sdk/tools/ecppl/Makefile
    sdk/tools/ecppll/Makefile
    sdk/demos/calc/Makefile
    sdk/demos/calcajax/Makefile
    sdk/demos/calcmvc/Makefile
    sdk/demos/cgi-getenv/Makefile
    sdk/demos/config/Makefile
    sdk/demos/controls/Makefile
    sdk/demos/cookie/Makefile
    sdk/demos/hello/Makefile
    sdk/demos/savepoint/Makefile
    sdk/demos/session/Makefile
    sdk/demos/sprintf/Makefile
    sdk/demos/strings/Makefile
    sdk/demos/subcomp/Makefile
    sdk/demos/upload/Makefile
    etc/Makefile
    tntnet-config
    ])

AC_OUTPUT

chmod +x tntnet-config
